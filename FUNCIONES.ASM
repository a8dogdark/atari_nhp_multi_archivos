RY
    .BYTE 0,0,0
LEN 
    .BYTE 0,0,0
MEMORIADISPONIBLE
    .BYTE 0,0,0
BANCOSDISPONIBLES
    .BYTE 0
ARCHIVOSAGRABAR
    .BYTE 0
BYTESPORBLOQUES
    .BYTE 0
VELOCIDADDEGRABACION
    .BYTE 0
GRABACIONCONAUDIO
    .BYTE 0
;************************************************
;FUNCION QUE NOS PERMITE PODER CONVERTIR UN BYTE
;EN ATASCII, USADO PARA INGRESO DE TITULOS Y
;FUENTE, NO TIENE LIMITACIONES MAYORES EN LAS
;PULSACIONES DEL TECLADO
;************************************************
ASCINT
	CMP #32
	BCC ADD64
	CMP #96
	BCC SUB32
	CMP #128
	BCC REMAIN
	CMP #160
	BCC ADD64
	CMP #224
	BCC SUB32
	BCS REMAIN
ADD64
	CLC
	ADC #64
	BCC REMAIN
SUB32
	SEC
	SBC #32
REMAIN
	RTS
;************************************************
;GENERA UNA LIMPIEZA TOTAL DEL DISPLAY DEL
;DIRECTORIO
;************************************************
CLS
	LDX # <??DIR
	LDY # >??DIR
	STX PCRSR
	STY PCRSR+1
	LDY #$00
	LDX #$00
?CLS
	LDA #$00
	STA (PCRSR),Y
	INY
	BNE ??CLS
	INX
	INC PCRSR+1
??CLS
	CPY #104	;$68
	BNE ?CLS
	CPX #$01
	BNE ?CLS
	RTS
;************************************************
;FUNCION QUE ABRE PERIFERICOS
;************************************************
OPEN
	LDX #$10
	LDA #$03
	STA $0342,X
	LDA # <?FUENTE
	STA $0344,X
	LDA # >?FUENTE
	STA $0345,X
	LDA #$04
	STA $034A,X
	LDA #$80
	STA $034B,X
	JSR $E456
	DEY
	BNE ESDIR
	RTS
ESDIR
    JMP DIRECTORIODEARCHIVOS
;************************************************
;FUNCION QUE CIERRA PERIFERICOS
;************************************************
CLOSE
	LDX #$10
	LDA #$0C
	STA $0342,X
	JMP $E456


;************************************************
;RUTINA QUE NOS PERMMITE PODER INGRESAR INFORMA-
;CION A UN CAMPO ESPECIFICO YA ANTES DECLARADO
;MOSTRANDO UN CURSOR EN FORMA PARPADEANTE
;************************************************
;
;************************************************
;CURSOR PARPADEANTE
;************************************************
FLSH
	LDY RY
	LDA (PCRSR),Y
	EOR #63
	STA (PCRSR),Y
	LDA #$10
	STA $021A
	RTS
;************************************************
;ABRE PERIFERICO TECLADO
;************************************************
OPENK
	LDA #255
	STA 764
	LDX #$10
	LDA #$03
	STA $0342,X
	STA $0345,X
	LDA #$26
	STA $0344,X
	LDA #$04
	STA $034A,X
	JSR $E456
	LDA #$07
	STA $0342,X
	LDA #$00
	STA $0348,X
	STA $0349,X
	STA RY
	RTS
;************************************************
;RUTINA QUE LEE LO TECLEADO
;************************************************
RUTLEE
	LDX # <FLSH
	LDY # >FLSH
	LDA #$10
	STX $0228
	STY $0229
	STA $021A
	JSR OPENK
GETEC
	JSR $E456
	CMP #$7E
	BNE C0
	LDY RY
	BEQ GETEC
	LDA #$00
	STA (PCRSR),Y
	LDA #63		;$3F
	DEY
	STA (PCRSR),Y
	DEC RY
	JMP GETEC
C0
	CMP #155	;$9B
	BEQ C2
	JSR ASCINT
	LDY RY
	STA (PCRSR),Y
	CPY #20		;#14
	BEQ C1
	INC RY
C1
	JMP GETEC
C2
	JSR CLOSE
	LDA #$00
	STA $021A
	LDY RY
	STA (PCRSR),Y
	RTS


MUESTROMEMORIADISPONIBLE
    JSR LIMPIOVAL
    LDA MEMORIADISPONIBLE
    STA VAL 
    LDA MEMORIADISPONIBLE+1
    STA VAL+1
    LDA MEMORIADISPONIBLE+2
    STA VAL+2
    JSR BIN2BCD
    LDX #7
    LDY #6
MUESTROMEMORIADISPONIBLE01
    LDA RESATASCII,X
    STA VEOMEMORIADISPONIBLE,Y
    DEX
    DEY
    BPL MUESTROMEMORIADISPONIBLE01
    RTS

MUESTROBANCOSDISPONIBLES
    JSR LIMPIOVAL
    LDA BANKOS
    STA VAL
    JSR BIN2BCD
    LDA RESATASCII+7
    STA VEOBANCOSDISPONIBLES+1
    LDA RESATASCII+6
    STA VEOBANCOSDISPONIBLES
    RTS

MUESTROARCHIVOSAGRABAR
    JSR LIMPIOVAL
    LDA ARCHIVOSAGRABAR
    STA VAL
    JSR BIN2BCD
    LDA RESATASCII+7
    STA VEOARCHIVOSAGRABAR
    RTS

LIMPIODATOS
    JSR MEMORIA
    LDA MEMORY
    STA MEMORIADISPONIBLE
    LDA MEMORY+1
    STA MEMORIADISPONIBLE+1
    LDA MEMORY+2
    STA MEMORIADISPONIBLE+2
    JSR MUESTROMEMORIADISPONIBLE
    JSR MUESTROBANCOSDISPONIBLES
    JSR MUESTROARCHIVOSAGRABAR
    RTS
;****************************
;FUNCION PRESIONA TECLA START
;****************************
ESSTART
    LDA CONSOL
    CMP #$06
    BNE ESSTART
ESSTART02
    LDA CONSOL
    CMP #$06
    BEQ ESSTART02
    RTS

LIMPIOCARGAARCHIVOS
    LDX #19
    LDA #$00
LIMPIOCARGAARCHIVOS01
    STA TITULO01,X
    STA TITULO02,X
    STA FUENTE,X
    STA TITULOGRANDE01,X
    STA TITULOGRANDE02,X
    DEX
    BPL LIMPIOCARGAARCHIVOS01
    LDX #39
LIMPIOCARGAARCHIVOS02
    STA MENSAJECARGA,X
    DEX
    BPL LIMPIOCARGAARCHIVOS02
    LDA #$10
    LDX #5
LIMPIOCARGAARCHIVOS03
    STA BYTES,X
    DEX
    BPL LIMPIOCARGAARCHIVOS03
    LDX #3
LIMPIOCARGAARCHIVOS04
    STA BLOQUES,X
    DEX
    BPL LIMPIOCARGAARCHIVOS04
    ;LDA #$3F
    ;STA TITULO01
    ;STA TITULO02
    ;STA FUENTE
    RTS